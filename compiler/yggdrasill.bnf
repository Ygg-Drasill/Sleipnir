<<
import (
    "github.com/Ygg-Drasill/Sleipnir/compiler/gocc/util"
    "github.com/Ygg-Drasill/Sleipnir/compiler/gocc/token"
    "github.com/Ygg-Drasill/Sleipnir/compiler/ast"
)
>>

/* Production rules */

/* Outer file scope */
ProgramDeclarationLst
    : NodeLst ConnLst   << ast.NewProgram($0, $1) >>
    ;

Node
    : "node" nodeId "{" NodeIn NodeOut NodeProc "}"     << ast.NewNode($Context, $1, $3, $4, $5) >>
    ;

Conn
    : ConnOut "->" ConnIn stmtEnd       << ast.NewConnection($0, $2) >>
    ;

ConnOut
    : nodeId "." out "." varId          << ast.NewJunction($0, $4) >>
    | nodeId "." out                    << ast.NewJunction($0, "") >>
    ;

ConnIn
    : nodeId "." in "." varId           << ast.NewJunction($0, $4) >>
    | nodeId "." in                     << ast.NewJunction($0, "") >>
    ;

NodeLst
    : Node          << ast.NewNodeList($0) >>
    | Node NodeLst  << ast.AppendNode($1, $0) >>
    ;

ConnLst
    : Conn          << ast.NewConnectionList($0) >>
    | Conn ConnLst  << ast.AppendConnection($1, $0) >>
    ;

/* Node scope */
Type : "int" | "bool" | "string" ;

NodeVar : in "." varId | out "." varId | varId  ;


NodeIn
    : in "{" DeclarationLst "}" << ast.NewScopeIn($Context) >>
    | empty
    ;

NodeOut
    : out "{" DeclarationLst "}" << ast.NewScopeOut($Context) >>
    | empty
    ;

NodeProc
    : "process" "{" StmtLst "}" << ast.NewScopeProc($Context) >>
    | "process" "{" "}"         << ast.NewScopeProc($Context) >>
    | empty
    ;

/* Statement productions */
DeclarationStmt
    	: Type varId stmtEnd                << ast.NewDeclaration($0, $1, nil, $Context) >>
    	| Type varId Assignment stmtEnd     << ast.NewDeclaration($0, $1, $2, $Context) >>
    	;

DeclarationLst
    	: DeclarationStmt DeclarationLst    << ast.AppendDeclaration($1, $0) >>
    	| DeclarationStmt                   << ast.NewDeclarationList($0) >>
	    ;

Condition
    	: LogicalExpr logicOp Condition
    	| LogicalExpr
    	;

Assignment : assignOp Expr ;

AssignStmt : NodeVar Assignment stmtEnd ;

ControlStmt: IfStmt | WhileStmt ;

IfStmt: "if" "(" Condition ")" "{" StmtLst "}" ;

WhileStmt : "while" "(" Condition ")" "{" StmtLst "}" ;

Stmt : DeclarationStmt | AssignStmt | ControlStmt ;

StmtLst : Stmt StmtLst | Stmt ;


/* Expressions */
Expr
    : AritExpr
    | LogicalExpr
    ;

LogicalExpr : NodeVar compOp NodeVar ;

AritExpr
    : AritExpr "+" Term
    | AritExpr "-" Term
    | Term
    ;

Term
    : Term "*" Factor
    | Term "/" Factor
    | Factor
    ;

Factor
    : int64 						<< util.IntValue($0.(*token.Token).Lit) >>
    | NodeVar
    ;
