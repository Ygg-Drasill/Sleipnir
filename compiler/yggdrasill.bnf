<<
import (
    "github.com/Ygg-Drasill/Sleipnir/compiler/gocc/util"
    "github.com/Ygg-Drasill/Sleipnir/compiler/gocc/token"
    "github.com/Ygg-Drasill/Sleipnir/compiler/ast"
)
>>

/* Production rules */

/* Outer file scope */
ProgramDeclarationList
    : NodeLst ConnLst   << ast.NewProgram($0, $1) >>
    ;

Node
    : node nodeId lcurly NodeIn NodeOut NodeProc rcurly     << ast.NewNode($Context, $1, $3, $4, $5) >>
    ;

Conn
    : ConnOut "->" ConnIn stmtEnd       << ast.NewConnection($0, $2) >>
    ;

ConnOut
    : nodeId period out period varId          << ast.NewJunction($0, $4) >>
    | nodeId period out                    << ast.NewJunction($0, "") >>
    ;

ConnIn
    : nodeId period in period varId           << ast.NewJunction($0, $4) >>
    | nodeId period in                     << ast.NewJunction($0, "") >>
    ;

NodeLst
    : Node          << ast.NewNodeList($0) >>
    | Node NodeLst  << ast.AppendNode($1, $0) >>
    ;

ConnLst
    : Conn ConnLst  << ast.AppendConnection($1, $0) >>
    | empty          << ast.NewConnectionList() >>
    ;

/* Node scope */
Type : int | bool | string ;

NodeVar
    : in period varId     << ast.NewNodeVar($0, $2) >>
    | out period varId    << ast.NewNodeVar($0, $2) >>
    | varId
    ;

NodeIn
    : in lcurly DeclarationList rcurly << ast.NewScopeIn($Context, $2) >>
    | empty
    ;

NodeOut
    : out lcurly DeclarationList rcurly << ast.NewScopeOut($Context, $2) >>
    | empty
    ;

NodeProc
    : process lcurly StmtLst rcurly << ast.NewScopeProc($Context, $2) >>
    | process lcurly "}"         << ast.NewScopeProc($Context, nil) >>
    | empty
    ;

/* Statement productions */
Identifier
    : NodeVar
    | varId
    ;

CompareOperator
    : eq | lt | gt | neq | and | or ;

InfixOperator
    : plus | minus | mul | div  /*arithmetic*/
    | CompareOperator /*logic*/
    ;


Expression
    : Expression InfixOperator Term
    | Term
    ;

DeclarationStmt
    : Type varId stmtEnd                << ast.NewDeclaration($Context, $0, $1, nil) >>
    | Type varId Assignment stmtEnd     << ast.NewDeclaration($Context, $0, $1, $2) >>
    ;

DeclarationList
    : DeclarationStmt DeclarationList    << ast.AppendDeclaration($1, $0) >>
    | empty                              << ast.NewDeclarationList() >>
    ;

Assignment
    : assign Expr   << $1, nil >>
    ;

AssignStmt
    : Identifier Assignment stmtEnd << $0, nil >>
    ;

ControlStmt
    : IfStmt        << $0, nil >>
    | WhileStmt     << $0, nil >>
    ;

IfStmt
    : if lp Expr rp lcurly StmtLst rcurly else lcurly StmtLst rcurly  << ast.NewIfStatement($2, $5, $9) >>
    ;

WhileStmt
    : while lp Expr rp lcurly StmtLst rcurly << ast.NewWhileStatement($2, $5) >>
    ;

Stmt
    : DeclarationStmt   << $0, nil >>
    | AssignStmt        << $0, nil >>
    | ControlStmt       << $0, nil >>
    ;

StmtLst
    : Stmt StmtLst  << ast.AppendStatement($1, $0) >>
    | Stmt          << ast.NewStatementList($0) >>
    | empty
    ;

/* Expressions */
Expr
    : AritExpr              << $0, nil >>
    | LogicalExpr           << $0, nil >>
    ;

LogicalExpr
    : NodeVar CompareOperator NodeVar    << ast.NewExpression($0, $1, $2) >>
    ;

AritExpr
    : AritExpr plus Term    << ast.NewExpression($0, $1, $2) >>
    | AritExpr minus Term   << ast.NewExpression($0, $1, $2) >>
    | Term                  << $0, nil >>
    ;

Term
    : Term mul Factor       << ast.NewExpression($0, $1, $2) >>
    | Term div Factor       << ast.NewExpression($0, $1, $2) >>
    | Factor                << $0, nil >>
    ;

Factor
    : lp Expression rp
    | int64 	<< util.IntValue($0.(*token.Token).Lit) >>
    | Identifier   << $0, nil >>
    ;
